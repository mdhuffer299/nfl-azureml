/* 
	Author:			Michael Huffer
	Description:	DML for team_history table used for Azure ML play type classification model training and testing.  
					This table contains aggregated play-by-play team statistics up to a game granularly.
					This script is part of a collection of SQL, R, Azure ML, and Power BI artifacts that have been prepared for a SQL Saturday presentation.
*/

USE [NFL]
GO

IF OBJECT_ID('dbo.team_history' , 'U') IS NOT NULL
TRUNCATE TABLE dbo.team_history;
GO

INSERT INTO dbo.team_history
(
	GAME_ID
	,POS_TEAM
	,DEF_TEAM
	,TEAM_TOTAL_YARDS
	,TEAM_TOTAL_TD
	,TEAM_TOTAL_PLAYS_RUN
	,TOTAL_SACKS_TAKEN
	,TOTAL_INT_THROWN
	,TOTAL_PASS_RIGHT
	,TOTAL_PASS_LEFT
	,TOTAL_PASS_MIDDLE
	,TOTAL_RUN_RIGHT
	,TOTAL_RUN_LEFT
	,TOTAL_RUN_MIDDLE
	,TOTAL_RUN_TACKLE_GAP
	,TOTAL_RUN_GUARD_GAP
	,TOTAL_RUN_END_GAP
	,HOME_TEAM
	,AWAY_TEAM
	,SEASON
)

SELECT
	SUB.GAME_ID
	,SUB.POS_TEAM
	,SUB.DEF_TEAM
	,SUM(SUB.YARDS_GAINED) AS TEAM_TOTAL_YARDS
	,SUM(CAST(SUB.TOUCHDOWN AS INT)) AS TEAM_TOTAL_TD
	,SUM(CAST(SUB.PLAY_ATTEMPTED AS INT)) AS TEAM_TOTAL_PLAYS_RUN
	,SUM(CAST(SUB.SACK AS INT)) AS TOTAL_SACKS_TAKEN
	,SUM(CAST(SUB.INTERCEPTION_THROWN AS INT)) AS TOTAL_INT_THROWN
	,SUM(SUB.PASS_RIGHT) AS TOTAL_PASS_RIGHT
	,SUM(SUB.PASS_LEFT) AS TOTAL_PASS_LEFT
	,SUM(SUB.PASS_MIDDLE) AS TOTAL_PASS_MIDDLE
	,SUM(SUB.RUN_RIGHT) AS TOTAL_RUN_RIGHT
	,SUM(SUB.RUN_LEFT) AS TOTAL_RUN_LEFT
	,SUM(SUB.RUN_MIDDLE) AS TOTAL_RUN_MIDDLE
	,SUM(SUB.RUN_TACKLE_GAP) AS TOTAL_RUN_TACKLE_GAP
	,SUM(SUB.RUN_GUARD_GAP) AS TOTAL_RUN_GUARD_GAP
	,SUM(SUB.RUN_END_GAP) AS TOTAL_RUN_END_GAP
	,SUB.HOME_TEAM
	,SUB.AWAY_TEAM
	,SUB.SEASON
FROM
(
	SELECT 
		[GAME_ID]
		,[POS_TEAM]
		,[DEF_TEAM]
		,YARDS_GAINED
		,TOUCHDOWN
		,PLAY_ATTEMPTED
		,SACK
		,INTERCEPTION_THROWN
		,CASE
			WHEN PASS_LOCATION = 'right' THEN 1
			ELSE 0
		END AS PASS_RIGHT
		,CASE
			WHEN PASS_LOCATION = 'left' THEN 1
			ELSE 0
		END AS PASS_LEFT
		,CASE
			WHEN PASS_LOCATION = 'middle' THEN 1
			ELSE 0
		END AS PASS_MIDDLE		
		,CASE
			WHEN RUN_LOCATION = 'right' THEN 1
			ELSE 0
		END AS RUN_RIGHT
		,CASE
			WHEN RUN_LOCATION = 'left' THEN 1
			ELSE 0
		END AS RUN_LEFT
		,CASE
			WHEN RUN_LOCATION = 'middle' THEN 1
			ELSE 0
		END AS RUN_MIDDLE
		,CASE
			WHEN RUN_GAP = 'tackle' THEN 1
			ELSE 0
		END AS RUN_TACKLE_GAP
		,CASE
			WHEN RUN_GAP = 'guard' THEN 1
			ELSE 0
		END AS RUN_GUARD_GAP
		,CASE
			WHEN RUN_GAP = 'end' THEN 1
			ELSE 0
		END AS RUN_END_GAP
		,[HOME_TEAM]
		,[AWAY_TEAM]
		,[SEASON]
	FROM [dbo].[play_by_play]
	WHERE HOME_TEAM = POS_TEAM
	OR AWAY_TEAM = POS_TEAM
) AS SUB
GROUP BY
	SUB.GAME_ID
	,SUB.POS_TEAM
	,SUB.DEF_TEAM
	,SUB.AWAY_TEAM
	,SUB.HOME_TEAM
	,SUB.SEASON
GO